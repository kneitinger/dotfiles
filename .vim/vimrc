"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Vim-Plug Initialization
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set encoding=utf-8
scriptencoding utf-8

" Install vimplug if not found
if has('nvim')
    if empty(glob('~/.local/share/nvim/site/autoload/plug.vim'))
      silent !curl -fLo ~/.local/share/nvim/site/autoload/plug.vim --create-dirs
        \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    endif
else
    if empty(glob('~/.vim/autoload/plug.vim'))
      silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
        \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
    endif
endif
autocmd VimEnter * PlugInstall --sync | source $MYVIMRC

call plug#begin('~/.vim/bundle')
Plug 'w0rp/ale'
Plug 'godlygeek/tabular'
Plug 'airblade/vim-gitgutter'
Plug 'neozenith/estilo-xoria256'
Plug 'gmoe/vim-faust'
Plug 'sirver/ultisnips'
Plug 'shougo/deoplete.nvim'
Plug 'zchee/deoplete-jedi'
Plug 'zchee/deoplete-go'
Plug 'junegunn/fzf.vim'
Plug 'lervag/vimtex'
Plug 'ambv/black'
Plug 'vimwiki/vimwiki'
Plug 'rust-lang/rust.vim'
Plug 'majutsushi/tagbar'
Plug 'autozimu/LanguageClient-neovim', {
    \ 'branch': 'next',
    \ 'do': 'bash install.sh',
    \ }
call plug#end()

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Basic Settings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if !has('nvim')
    set t_Co=256        " 256 color mode
endif

" No bells
set noerrorbells
set novisualbell
set t_vb=
set timeoutlen=500

set autoread        " Automatically reload files upon change
set history=1000

" buffers need not be written before switching
" http://derekwyatt.org/2009/08/20/the-absolute-bare-minimum/
set hidden

" Open split and vsplit below and to the right, respectively
set splitbelow
set splitright

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Appearance
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

try
    colorscheme fairlyfloss
endtry

" Regenerate colorscheme file if it is written to
au! BufWrite */colorscheme_gen.erb !erb -T - % > $HOME/.vim/colors/fairlyfloss.vim

" Ensure that highlight commands are run after colorscheme is loaded
" run :so ~/.vim/color_names.vim to find desired values
au! VimEnter  * hi Comment cterm=italic ctermfg=14
              \ | hi StatusLine cterm=bold,italic ctermbg=4 ctermfg=15
              \ | hi User1 ctermbg=14
              \ | hi Normal guibg=NONE ctermbg=NONE
              \ | hi LineNr ctermfg=12
              "\ | hi MatchParen  ctermfg=15 ctermbg=131

set laststatus=2    " always show status bar

" status: [paste indicator][buffer num][filename][modified][readonly]
set statusline=%{HasPaste()}[%n][%<%F]%m%r%=
" status: spacer [filetype]['Col:'column]['Line:' n/N:percentage through file]
set statusline+=%y[Col:%c][Line:%l/%L:%p%%]

set tabline=%!TabLine()

set number          " Show current line number.
set relativenumber  " All other line numbers are distance from current line
set cursorline      " Highlight current line
set lazyredraw      " Don't redraw screen until macro completes
set scrolloff=2     " Pad scrolls with 2 lines of context
set wildmenu        " Show bar of command autocompletes
set wildignore=*.o,*.out,*~,*.pyc,*/.git/*,*/.hg/*,*/.svn/*,*/.DS_Store


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Habit Breaking
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if !empty($VIM_HARD_MODE)
    noremap hh <nop>
    noremap jj <nop>
    noremap kk <nop>
    noremap ll <nop>
endif

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Mappings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
let g:mapleader = '\'

" Copy/cut/paste to system clipboard
vnoremap <leader>c "+y
nnoremap <leader>c "+y$

inoremap <leader>v <ESC>"+pa
vnoremap <leader>v "+p
nnoremap <leader>v "+p

vnoremap <leader>x "+d
nnoremap <leader>x "+D

" Make motion on wrapped lines intuitive
noremap <silent> j gj
noremap <silent> k gk
" Save on 2 Escs
map <Esc><Esc> :w<CR>
" No Ex mode!!
noremap Q <Nop>
" Make Y behave like C/D, instead of yy
map Y y$
" Clear search highlights upon screen-clear
nnoremap <C-L> :nohl<CR><C-L>
" Reselect visual block selection after indent
vnoremap < <gv
vnoremap > >gv

" Switch CWD to the directory of the open buffer
map <leader>cd :cd %:p:h<cr>:pwd<cr>

" Toggle paste mode on and off
set pastetoggle=
map <leader>pp :setlocal paste!<cr>


" * or # search for selection in visual mode
vnoremap <silent> * :<C-u>call VisualSelection('', '')<CR>/<C-R>=@/<CR><CR>
vnoremap <silent> # :<C-u>call VisualSelection('', '')<CR>?<C-R>=@/<CR><CR>

" Edit .vimrc
nmap <silent> <leader>ev :e $MYVIMRC<cr>
" Source .vimrc
nmap <silent> <leader>sv :so $MYVIMRC<cr>:doau VimEnter<cr>

" Spell check on/off
nmap <silent> <leader>sp :setlocal spell!<CR>
" Spell check jump
map <leader>sn ]s
map <leader>sp [s
" Spell check add
map <leader>sa zg
" Spell check suggestions
map <leader>s? z=

set mouse=a         " Enable the use of the mouse.

" Remove trailing whitespace
" http://vim.wikia.com/wiki/Remove_unwanted_spaces
nmap <leader>w :cal StripTrailingWhitespace()<CR>

" Echo the colorscheme element of the current text
nmap <leader>ccc :call <SID>SynStack()<CR>

" Search Options
set hlsearch        " Highlight all search matches
set incsearch       " Match as search pattern is being typed
set ignorecase      " Ignore case in search patterns ...
set smartcase       " ...unless the search term has an upper case character



" Tabs, spaces and backspaces
" set backspace=2     " Influences the working of <BS>, <Del>, CTRL-W
                    " and CTRL-U in Insert mode. This is a list of items,
                    " separated by commas. Each item allows a way to backspace
                    " over something.

set autoindent      " Copy indent from current line when starting a new line
set smartindent     " Make intuitive indent choices for some contexrs

set tabstop=4       " Number of spaces that a <Tab> in the file counts for.
set shiftwidth=4    " Number of spaces to use for each step of (auto)indent.
set expandtab       " Use the appropriate number of spaces to insert a <Tab>.
set smarttab        " Be smart about auto tabbing

set showmatch       " When a bracket is inserted, show the matching one
set matchtime=1     " Tenths of a second to show the match

"set textwidth=79    " Line auto-break column threshold

"set formatoptions=c,r,q,j

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Filetype-Specific Settings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Force certain extensions to be recognized
augroup filetype
    au! BufRead,BufNewFile *.ll     set filetype=llvm
    au! BufRead,BufNewFile *.fst    set filetype=faust
    au! BufRead,BufNewFile *.dsp    set filetype=faust
    au! BufRead,BufNewFile *.lib    set filetype=faust
    au! BufRead,BufNewFile *.lhs    set textwidth=74
    au! BufRead,BufNewFile *.lhs    set colorcolumn=76
    au! BufRead,BufNewFile *.yml    set ts=2 shiftwidth=2
augroup END

autocmd FileType tex setlocal shiftwidth=2 tabstop=2

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Miscellaneous Settings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" W allows writing a file that you forgot to sudo vim on
"   - existence check lets you :source vimrc without error
if !exists('sudowrite')
    command W w !sudo tee % > /dev/null
    let g:sudowrite=1
endif

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Backup/Swap/Persistence Settings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
try
    set undodir=~/.vim/cache/undo//
    set undofile
catch
endtry

try
    set backupdir=~/.vim/cache/backup//
    set backup
catch
endtry

try
    set directory=~/.vim/cache/swap//
    set swapfile
catch
endtry

" Return to last edit position when opening files (You want this!)
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
au BufEnter * set conceallevel=0  " Do not hide quotes, markdown formatters, etc.

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Function definitions
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! StripTrailingWhitespace()
    if !&binary && &filetype != 'diff'
        normal mz
        normal Hmy
        %s/\s\+$//e
        normal 'yz<CR>
        normal `z
        retab
    endif
endfunction

" For use in status line
function! HasPaste()
    if &paste
        return '[PASTE]'
    endif
    return ''
endfunction

function! KVimwikiDiaryGenerateLinks()
    if &filetype == 'vimwiki' && @% =~ "diary.md"
        " Clear previous diary links
        if line('$') > 2
            normal 3GdG
        endif
        " Run stock gen
        VimwikiDiaryGenerateLinks
        " Change intent so as to not interpret as code/mono
        %s/  \* \[2/- [2/g
        " Remove header that stock gen inserts
        %s/# Diary\n\n//g
    endif
endfunction
" Give it a nice command with quick tab completion
command! KyleVimwikiDiaryGenLinks call KVimwikiDiaryGenerateLinks()

" Load freebsd style functions, mapped to <leader>f
source $HOME/.vim/scripts/freebsd.vim

" Echo the colorscheme element of the current text
function! <SID>SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunc

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Terminal Settings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if has('nvim')
    au TermOpen * set nonumber norelativenumber
endif
tnoremap <C-\> <C-\><C-n>
tnoremap <A-h> <C-\><C-N><C-w>h
tnoremap <A-j> <C-\><C-N><C-w>j
tnoremap <A-k> <C-\><C-N><C-w>k
tnoremap <A-l> <C-\><C-N><C-w>l
inoremap <A-h> <C-\><C-N><C-w>h
inoremap <A-j> <C-\><C-N><C-w>j
inoremap <A-k> <C-\><C-N><C-w>k
inoremap <A-l> <C-\><C-N><C-w>l
nnoremap <A-h> <C-w>h
nnoremap <A-j> <C-w>j
nnoremap <A-k> <C-w>k
nnoremap <A-l> <C-w>l

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugin Settings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" Tabular
" http://vimcasts.org/episodes/aligning-text-with-tabular-vim/

" UltiSnips
let g:UltiSnipsSnippetDirectories=[$HOME.'/.vim/UtilSnips']
let g:UltiSnipsExpandTrigger="<c-\\>"
let g:UltiSnipsJumpForwardTrigger='<c-j>'
let g:UltiSnipsJumpBackwardTrigger='<c-k>'

" Deoplete
let g:deoplete#enable_at_startup = 1
" Use tab for suggestion selection
inoremap <expr><tab> pumvisible() ? "\<c-n>" : "\<tab>"
"let g:python2_host_prog = '/home/leaf/.pyenv/neovim2/bin/python'
let g:python3_host_prog = '/home/leaf/.vim/.venv/bin/python3'

" vim-terraform
let g:terraform_align=1
let g:terraform_fmt_on_save=1

" vimwiki
let g:vimwiki_list = [{'path': '$HOME/notes',
    \ 'syntax': 'markdown',
    \ 'ext': '.md',
    \ 'path_html': '$HOME/notes/html',
    \ 'custom_wiki2html': '$HOME/.vim/scripts/wiki2html.sh'}]
let g:vimwiki_conceallevel = 2
" Only count notebook md files as vimwiki filetype
let g:vimwiki_global_ext = 0

au BufRead,BufNewFile ~/notes/**.md call SetVimwikiOptions()
function! SetVimwikiOptions()
    setlocal autoread
    cd ~/notes
    au! CursorHold ~/notes/* checktime
    let &titlestring = '%y'
    setlocal title
    setlocal noswapfile
    setlocal tabstop=2
    setlocal shiftwidth=2
    au! BufWrite ~/notes/* silent VimwikiAll2HTML
endfunction

" vimtex
let g:vimtex_view_method='zathura'
let g:vimtex_compiler_latexmk = {'build_dir' : './build'}

" fzf
nnoremap <leader>f :Files<CR>
nnoremap <leader>b :Buffers<CR>
nnoremap <leader>l :Lines<CR>
nnoremap <leader>r :Rg<CR>

" ALE
au BufRead,BufNewFile ~/.ipylogs/**.py ALEDisable

" Rust.vim
let g:rustfmt_autosave = 1

" Language Client
let g:LanguageClient_serverCommands = {
    \ 'rust': ['~/.cargo/bin/rustup', 'run', 'stable', 'rls'],
    \ }
set completeopt-=preview
